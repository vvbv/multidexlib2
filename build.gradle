/*
 * multidexlib2 - Copyright 2015-2022 Rodrigo Balerdi
 * (GNU General Public License version 3 or later)
 *
 * multidexlib2 is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published
 * by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

def dexlib2Version = '2.5.3-a3836654'

group = 'app.revanced'
version = dexlib2Version

ext.mainArtifact = 'multidexlib2'
ext.artifactName = mainArtifact

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def githubUsername = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
def githubPassword = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/revanced/smali")
        credentials {
            username = githubUsername
            password = githubPassword
        }
    }
}

dependencies {
    api 'app.revanced:dexlib2:' + dexlib2Version
    api 'com.google.guava:guava:31.1-android'
}

apply from: 'configure-artifacts.gradle'

apply from: 'configure-publishing.gradle'

publishing {
    publications {
        gpr(MavenPublication) {
            artifactId = mainArtifact
            from components.java
            pom {
                name = artifactName
                configurePom it
            }
        }
    }
}

apply from: 'check-copyright.gradle'

wrapper.distributionType = Wrapper.DistributionType.ALL
